WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }

file = _{ SOI ~ (statement? ~ NEWLINE)* ~ EOI }
statement = _{ define | capability | position | property | pixelmap | animdef | animframe | mapping }

string = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
reserved = _{ "\"" | ":" | "," | ";" | "[" | "]" | "(" | ")"  }
char = @{ !(WHITESPACE | reserved | op) ~ ANY }
name_char = @{ LETTER | NUMBER | CONNECTOR_PUNCTUATION }

name = @{ (!NUMBER ~ name_char+) }
word = @{ char+ } 
value = @{ string | char* }

number = @{ "0x" ~ ASCII_HEX_DIGIT+ | NUMBER+ } 
id = { string | number }
range = { "[" ~ id ~ "-" ~ id ~ "]" }

time = { NUMBER ~ ("s" | "ms" | "us") }

key_state = { "P" | "UP" | "UR" | "R" | "H" | "O" }
state = { "On" | "D" | "Off" }
layer_type = { "Layer" | "LayerShift" | "LayerLatch" | "LayerLock" }
charcode = { "'" ~ char ~ "'" }
scancode = { "S" ~ (range | id) }
usbcode = { "U" ~ (range | id) }
consumer = { "CON" ~ id }
system = { "SYS" ~ id }
indicator = { "I" ~ id }
layer = { layer_type ~ "[" ~ number ~ "]" }
trig = { "T[" ~ number ~ "," ~ number ~ "]" }
key = _{ scancode | usbcode }

kv = { word ~ (":" ~ word)? }
kvmap = { kv ~ ("," ~ kv)* }

index = { NUMBER? }
array = { name ~ "[" ~ index ~ "]" }
function = { name ~ "(" ~ kvmap? ~ ")" }

lhs = _{ array | name | string }
property = { lhs ~ "=" ~ value ~ ";" }
define = { name ~ "=>" ~ value ~ ";" }
capability = { name ~ "=>" ~ function ~ ";" }
position = { pixel ~ "<=" ~ kvmap ~ ";" }

timing = { key_state ~ (":" ~ time)? }
timings = { timing ~ ("," ~ timing)* }

key_trigger = { key ~ ("(" ~ (timings | NUMBER) ~ ")") | key }
combo = _{ key_trigger ~ ("+" ~ key_trigger)+ }
sequence = _{ (combo | key_trigger) ~ ("," ~ (combo | key_trigger))+ }

indicator_trigger = { indicator | indicator ~  ("(" ~ state ~ ")") }
layer_trigger = { layer | layer ~ ("(" ~ state ~ ")") }
generic_trigger = { trig | trig ~ ("(" ~ state ~ ")") }
trigger = { sequence | combo | key_trigger | layer_trigger | indicator_trigger | generic_trigger | animation }


result = { sequence | usbcode | consumer | system | generic_trigger | function | color | "None" }


binding = { ":" | "::" | ":+" | ":-" | "i:" | "i::" | "i:+" | "i:-" }
mapping = { trigger ~ binding ~ result ~ ";" }

pixel = _{ "P[" ~ number ~ "]" }
pixeldef = { pixel ~ "(" ~ kvmap ~ ")" }
pixelmap = { pixeldef ~ ":" ~ (scancode | "None") ~ ";" }

animation = { "A[" ~ name ~ "]" }
animdef = { animation ~ "<=" ~ kvmap ~ ";" }

op = { "+" | "-" | "+:" | "-:" | "<<" | ">>" }
channel = { op? ~ number }
channels = { channel ~ ("," ~ channel)* }
color = { "P[" ~ kvmap ~ "](" ~ channels ~ ")" }
colors = { color ~ ("," ~ color)* }

frame = { "A[" ~ name ~ "," ~ number ~ "]" }
animframe = { frame ~ "<=" ~ colors ~ ";" }
