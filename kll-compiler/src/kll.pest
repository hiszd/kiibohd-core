WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }

file = { SOI ~ (statement? ~ NEWLINE)* ~ EOI }
statement = { define | capability | position | property | pixelmap | animdef | animframe | mapping }

string = { ("\"" ~ (!"\"" ~ ANY)* ~ "\"") | ("'" ~ (!"'" ~ ANY)* ~ "'") }
reserved = _{ "\"" | ":" | "," | ";" | "[" | "]" | "(" | ")"  }
char = @{ !(WHITESPACE | reserved) ~ ANY }
name_char = @{ LETTER | NUMBER | CONNECTOR_PUNCTUATION }

name = @{ name_char+ }
word = @{ char+ } 
value = @{ string | char* }

number = @{ "0x" ~ ASCII_HEX_DIGIT+ | NUMBER+ } 
id = _{ string | number }
range = { "[" ~ id ~ ("-" ~ id)? ~ "]" }

time = { NUMBER ~ ("s" | "ms" | "us") }

//key_state = { "P" | "UP" | "UR" | "R" | "H" | "O" }
state = { "On" | "D" | "Off" }
layer_type = { "LayerShift" | "LayerLatch" | "LayerLock" | "Layer" }
charcode = { ("'" | "u'") ~ (!"'" ~ ANY) ~ "'" }
pixel = { "P" ~ (range | id) }
pixellayer = _{ "PL" ~ (range | id) }
scancode = { "S" ~ (range | id) }
unicode = { "U+" ~ value }
unistr = { "u" ~ string }
usbcode = { "U" ~ (range | id) }
consumer = { "CONS" ~ (range | id) }
system = { "SYS" ~ (range | id) }
indicator = { "I" ~ (range | id) }
layer = { layer_type ~ range }
trig = { "T[" ~ number ~ "," ~ number ~ "]" }
none = { "None" }
key = { scancode | charcode | usbcode }

kv = { word ~ (":" ~ word)? }
kvmap = { kv ~ ("," ~ kv)* }

array = { name ~ "[" ~ number? ~ "]" }
function = { name ~ "(" ~ kvmap? ~ ")" }

//rhs = { (!reserved ~ ANY)* }
rhs = { value  ~ (" " ~ value)* }
lhs = _{ array | name | string }
property = { lhs ~ "=" ~ rhs ~ ";" }
define = { name ~ "=>" ~ value ~ ";" }
capability = { name ~ "=>" ~ function ~ ";" }
position = { (scancode | pixel) ~ "<=" ~ kvmap ~ ";" }

//timing = { key_state ~ (":" ~ time)? }
//timings = { timing ~ ("," ~ timing)* }

key_trigger = { key ~ ("(" ~ kvmap ~ ")") | key }
combo = _{ key_trigger ~ ("+" ~ key_trigger)+ }
sequence = _{ (combo | key_trigger) ~ ("," ~ (combo | key_trigger))+ }

indicator_trigger = { indicator | indicator ~  ("(" ~ state ~ ")") }
layer_trigger = { layer | layer ~ ("(" ~ state ~ ")") }
generic_trigger = { trig | trig ~ ("(" ~ kvmap ~ ")") }
trigger = { sequence | combo | key_trigger | layer_trigger | indicator_trigger | generic_trigger | animation }


result = { sequence | charcode | unicode | unistr | usbcode | consumer | system | generic_trigger | layer_trigger | function | pixelval | string | none }


binding = { "::" | ":+" | ":-" | ":" | "i::" | "i:+" | "i:-" | "i:" }
mapping = { trigger ~ binding ~ result ~ ";" }

pixeldef = _{ (pixellayer | pixel) ~ "(" ~ kvmap ~ ")" }
pixelmap = { pixeldef ~ ":" ~ (scancode | none) ~ ";" }

animation = _{ "A[" ~ name ~ "]" }
animdef = { animation ~ "<=" ~ kvmap ~ ";" }

op = { "+" | "-" | "+:" | "-:" | "<<" | ">>" }
channel = { op? ~ number }
channels = _{ channel ~ ("," ~ channel)* }
pixelval = { "P[" ~ kvmap ~ "](" ~ channels ~ ")" }
pixelvals = _{ pixelval ~ ("," ~ pixelval)* }

frame = _{ "A[" ~ name ~ "," ~ number ~ "]" }
animframe = { frame ~ "<=" ~ pixelvals ~ ";" }
