WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }

file = { SOI ~ (statement? ~ NEWLINE)* ~ EOI }
statement = { define | capability | position | property | pixelmap | animdef | animframe | mapping }

string = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
reserved = _{ "\"" | ":" | "," | ";" | "[" | "]" | "(" | ")"  }
char = @{ !(WHITESPACE | reserved | op) ~ ANY }
name_char = @{ LETTER | NUMBER | CONNECTOR_PUNCTUATION }

name = @{ (!NUMBER ~ name_char+) }
word = @{ char+ } 
value = @{ string | char* }

number = @{ "0x" ~ ASCII_HEX_DIGIT+ | NUMBER+ } 
id = { string | number }
range = { "[" ~ id ~ "-" ~ id ~ "]" }

time = { NUMBER ~ ("s" | "ms" | "us") }

//key_state = { "P" | "UP" | "UR" | "R" | "H" | "O" }
state = { "On" | "D" | "Off" }
layer_type = { "Layer" | "LayerShift" | "LayerLatch" | "LayerLock" }
charcode = { "'" ~ char ~ "'" }
scancode = { "S" ~ (range | id) }
usbcode = { "U" ~ (range | id) }
consumer = { "CON" ~ id }
system = { "SYS" ~ id }
indicator = { "I" ~ id }
layer = { layer_type ~ "[" ~ number ~ "]" }
trig = { "T[" ~ number ~ "," ~ number ~ "]" }
none = { "None" }
key = { scancode | usbcode }

kv = { word ~ (":" ~ word)? }
kvmap = { kv ~ ("," ~ kv)* }

index = _{ number? }
array = { name ~ "[" ~ index ~ "]" }
function = { name ~ "(" ~ kvmap? ~ ")" }

lhs = _{ array | string }
property = { lhs ~ "=" ~ value ~ ";" }
define = { name ~ "=>" ~ value ~ ";" }
capability = { name ~ "=>" ~ function ~ ";" }
position = { pixel ~ "<=" ~ kvmap ~ ";" }

//timing = { key_state ~ (":" ~ time)? }
//timings = { timing ~ ("," ~ timing)* }

key_trigger = { key ~ ("(" ~ kvmap ~ ")") | key }
combo = _{ key_trigger ~ ("+" ~ key_trigger)+ }
sequence = _{ (combo | key_trigger) ~ ("," ~ (combo | key_trigger))+ }

indicator_trigger = { indicator | indicator ~  ("(" ~ state ~ ")") }
layer_trigger = { layer | layer ~ ("(" ~ state ~ ")") }
generic_trigger = { trig | trig ~ ("(" ~ state ~ ")") }
trigger = { sequence | combo | key_trigger | layer_trigger | indicator_trigger | generic_trigger | animation }


result = { sequence | usbcode | consumer | system | generic_trigger | function | pixelval | none }


binding = { ":" | "::" | ":+" | ":-" | "i:" | "i::" | "i:+" | "i:-" }
mapping = { trigger ~ binding ~ result ~ ";" }

pixel = _{ "P[" ~ number ~ "]" }
pixeldef = _{ pixel ~ "(" ~ kvmap ~ ")" }
pixelmap = { pixeldef ~ ":" ~ (scancode | none) ~ ";" }

animation = _{ "A[" ~ name ~ "]" }
animdef = { animation ~ "<=" ~ kvmap ~ ";" }

op = { "+" | "-" | "+:" | "-:" | "<<" | ">>" }
channel = { op? ~ number }
channels = _{ channel ~ ("," ~ channel)* }
pixelval = { "P[" ~ kvmap ~ "](" ~ channels ~ ")" }
pixelvals = _{ pixelval ~ ("," ~ pixelval)* }

frame = _{ "A[" ~ name ~ "," ~ number ~ "]" }
animframe = { frame ~ "<=" ~ pixelvals ~ ";" }
